/* PRINCO1.SAS  :  PRINCIPAL COMPONENT ANALYSIS OF CRIME DATA */
  OPTIONS LS=80;
DATA CRIME;
  TITLE 'CRIME RATES PER 100,000 POPULATION BY STATE';
  INFILE 'C:\Users\Admin\Desktop\°ø°³°­ÁÂ_14\dataset\Crime.DAT';
  INPUT STATE $1-15 MURDER RAPE ROBBERY ASSAULT
          BURGLARY LARCENY AUTO;
RUN;
PROC PRINCOMP OUT=CRIMCOMP;
RUN;
PROC SORT DATA=CRIMCOMP;
  BY PRIN1;
RUN;
PROC PRINT;
  ID STATE;
  VAR PRIN1 PRIN2 MURDER RAPE ROBBERY ASSAULT
       BURGLARY LARCENY AUTO;
  TITLE2 'STATES LISTED IN ORDER OF OVERALL CRIME RATE';
  TITLE3 'AS DETERMINED BY THE FIRST PRINCIPAL COMPONENT';
RUN;
PROC SORT;
  BY PRIN2;
PROC PRINT;
  ID STATE;
  VAR PRIN1 PRIN2 MURDER RAPE ROBBERY ASSAULT
       BURGLARY LARCENY AUTO;
  TITLE2 'STATES LISTED IN ORDER OF PROPERTY VS. VIOLENT CRIME';
  TITLE3 'AS DETERMINED BY THE SECOND PRINCIPAL COMPONENT';
RUN;
PROC PLOT;
  PLOT PRIN2*PRIN1=STATE / VPOS=23;
  TITLE2 'PLOT OF THE FIRST TWO PRINCIPAL COMPONENTS';
RUN;
QUIT;
